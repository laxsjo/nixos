{ config, pkgs, ... }:

let
  # The content of `workman-se.xkb` was created by having the following configuration active and dumping it with `setxkbmap -print > some_file.xkb`
  # The only difference is the `partial` line and `xkb_symbols` block were added to replace `xkb_symbols { include "pc+ca(multi)+inet(evdev)+ctrl(swapcaps)+eurosign(e)+nbsp(none)" };`
  #   services.xserver = {
  #     # Set keyboard layout to Canadian Multilingual
  #     layout = "ca";
  #     xkbVariant = "multi";
  #     # AltGr + e for â‚¬, Swap Left Control and Caps Lock, AltGr + Space produces a normal space instead of a non-breakable space
  #     xkbOptions = "eurosign:e,ctrl:swapcaps,nbsp:none";
  #   };
  # ^ outdated
  xkbSymbols = ./workman-se.xkb;
  # Dummy package which always succeeds.
  unitPkg = pkgs.runCommand "" { } "touch $out";
in
{
  services.xserver = {
    enable = true;
    xkb = {
      layout = "se-workman";
      extraLayouts.se-workman = {
        description = "Swedish layout with workman letters";
        languages = [
          "eng"
          "swe"
        ];
        symbolsFile = xkbSymbols;
      };
    };
  };

  # Configure Kanata
  services.kanata = {
    enable = true;
    keyboards."layout".configFile = ./layout.kbd;
  };
  # Check that the service "kanata-layout", has indeed been created by
  # `services.kanata`.
  # I'm not sure if this is a valid use of `system.checks`... We can't add the
  # assert to the `systemd.services` assignment as that results in infinite
  # recursion.
  system.checks = [
    (
      assert config.systemd.services."kanata-layout".enable;
      unitPkg
    )
  ];
  # Override the Kanata Systemd service generated by services.kanta.enable.
  systemd.services."kanata-layout" = {
    unitConfig = {
      # Prevent unit from being restarted if it fails more than 5 times
      # within 60 seconds.
      "StartLimitIntervalSec" = "60s";
      "StartLimitBurst" = 5;
    };
    serviceConfig = {
      # Restart after 1 second on failure
      "Restart" = "on-failure";
      "RestartSec" = "1s";
    };
  };

  # Allows the uinput group to access keyboards via the uinput subsystem.
  # Usefull to run the Kanata CLI manually
  services.udev.packages = [
    (pkgs.writeTextFile {
      name = "uinput";
      destination = "/etc/udev/rules.d/99-uinput.rules";
      text = ''
        KERNEL=="uinput", MODE="0660", GROUP="uinput", OPTIONS+="static_node=uinput"
      '';
    })
  ];
  # Add myself to the uinput group
  users.users."rasmus".extraGroups = [
    "uinput"
  ];
}
